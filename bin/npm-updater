#!/usr/bin/perl

# Created on: 2016-06-08 08:23:05
# Create by:  Ivan Wills
# $Id$
# $Revision$, $HeadURL$, $Date$
# $Revision$, $Source$, $Date$

use strict;
use warnings;
use version;
use Scalar::Util;
use List::Util qw/max/;
#use List::MoreUtils;
use Getopt::Long;
use Pod::Usage;
use Data::Dumper qw/Dumper/;
use English qw/ -no_match_vars /;
use FindBin qw/$Bin/;
use Path::Tiny;
use JSON::XS qw/decode_json/;
use WWW::Mechanize::Cached;
use CHI;
use Term::ANSIColor qw/colored/;

our $VERSION = version->new('0.0.1');
my ($name)   = $PROGRAM_NAME =~ m{^.*/(.*?)$}mxs;
my $mech;

my %option = (
    cache   => '/tmp/npmupdater',
    verbose => 0,
    man     => 0,
    help    => 0,
    VERSION => 0,
);

sub sorter {
    my $A = $a;
    my $B = $b;
    $A =~ s/(\d+)/sprintf "%09i", $1/egxms;
    $B =~ s/(\d+)/sprintf "%09i", $1/egxms;
    return $A cmp $B;
}

main();
exit 0;

sub main {
    Getopt::Long::Configure('bundling');
    GetOptions(
        \%option,
        'cache|c=s',
        'pre_release|pre-release|p',
        'update|u=s',
        'verbose|v+',
        'man',
        'help',
        'VERSION!',
    ) or pod2usage(2);

    if ( $option{'VERSION'} ) {
        print "$name Version = $VERSION\n";
        exit 1;
    }
    elsif ( $option{'man'} ) {
        pod2usage( -verbose => 2 );
    }
    elsif ( $option{'help'} ) {
        pod2usage( -verbose => 1 );
    }
    elsif ($option{update}) {
        $option{update} = lc $option{update};
        if ($option{update} ne 'patch' && $option{update} ne 'minor' && $option{update} ne 'major') {
            warn "The option --update takes one of major, minor or patch!\n";
            pod2usage( -verbose => 1 );
        }
    }


    # do stuff here
    $mech = WWW::Mechanize::Cached->new(
        cache => CHI->new(
            driver   => 'File',
            root_dir => $option{cache},
        ),
    );
    my $package_json = @ARGV && -f $ARGV[0] ? shift @ARGV : 'package.json';
    my $package      = decode_json(scalar path($package_json)->slurp);
    my @types        = qw/dependencies devDependencies optionalDependencies peerDependencies/;
    my %dependencies;

    for my $type (@types) {
        for my $dependency ( keys %{ $package->{$type} || {} } ) {
            my $details = package_details($dependency);
            warn $@ if $option{verbose} && $@;
            undef $@;
            my $current = path('node_modules', $dependency, 'package.json');
            my @versions = sort keys %{ $details->{versions} };

            my $versions = {
                type      => $type =~ /^([a-z]+)Dependencies$/ ? $1 : '',
                $type     => 1,
                defined   => $package->{$type}{$dependency},
                available => @versions && ( sort sorter grep {$option{pre_release} ? 1 : !/[a-zA-Z-]/} @versions )[-1],
            };

            if ( -f $current ) {
                my $actual = decode_json(scalar $current->slurp);
                $versions->{actual} = $actual->{version};
            }

            next if !$option{verbose}
                && $versions->{available}
                && $versions->{actual}
                && $versions->{available} eq $versions->{actual};

            $dependencies{$type}{$dependency} = $versions;
        }

    }

    for my $type (@types) {
        my %max = (
            name      => (max (length "module"   , map {length $_} keys %{$dependencies{$type}})),
            type      => (max (length "type"     , map {length($dependencies{$type}{$_}{type}      // '')} keys %{$dependencies{$type}})),
            defined   => (max (length "defined"  , map {length($dependencies{$type}{$_}{defined}   // '')} keys %{$dependencies{$type}})),
            actual    => (max (length "actual"   , map {length($dependencies{$type}{$_}{actual}    // '')} keys %{$dependencies{$type}})),
            available => (max (length "available", map {length($dependencies{$type}{$_}{available} // '')} keys %{$dependencies{$type}})),
        );

        my $start = sprintf "%s\n  %-$max{name}s %$max{defined}s %$max{actual}s %$max{available}s\n", $type, 'Module', 'Defined', 'Actual', 'Available';

        for my $dependency (sort keys %{$dependencies{$type}}) {
            print $start;
            $start = '';

            my @diff_actual = split /[.-]/, $dependencies{$type}{$dependency}{actual} || '';
            my @diff_available = split /[.-]/, $dependencies{$type}{$dependency}{available};
            my @colors = qw/red yellow blue/;
            my $color = 'white';
            for my $i (0 .. $#colors) {
                if ( $diff_actual[$i] && $diff_available[$i] && $diff_actual[$i] ne $diff_available[$i] ) {
                    $color = $colors[$i];
                    last;
                }
            }
            printf "  %-$max{name}s %$max{defined}s %$max{actual}s %s\n",
                $dependency,
                $dependencies{$type}{$dependency}{defined},
                $dependencies{$type}{$dependency}{actual} || ' ',
                colored([$color], sprintf "%$max{available}s", $dependencies{$type}{$dependency}{available} || ' ');
        }
        print "\n";

    }

    if ($option{update}) {
        update(\%dependencies);
    }

    return;
}

sub update {
    my ($dependencies) = @_;

    my $match = $option{update} eq 'patch' ? qr/^\W?(\d+[.]\d+[.])\d+$/
        :   $option{update} eq 'minor'     ? qr/^\W?(\d+[.])\d+[.]\d+$/
        :   $option{update} eq 'major'     ? qr/^\W?(q)\d+[.]\d+[.]\d+$/
        :                                    qr/(.*)/;
    my $pkg = path('package.json')->slurp();

    for my $type (keys %$dependencies) {
        for my $package (keys %{$dependencies->{$type}}) {
            my %ver = %{$dependencies->{$type}{$package}};

            # skip if no change
            next if $ver{defined} eq $ver{available};
            my ($defined) = $ver{defined} =~ /$match/xms;
            my ($available) = $ver{available} =~ /$match/xms;

            if ((!defined $defined && !defined $available) ||
                (defined $defined && defined $available && $defined eq $available)
            ) {
                $pkg =~ s/"$package"\s*:\s*"$ver{defined}"/"$package": "$ver{available}"/gxms;
                if ($pkg !~ /"$package": "$ver{available}"/) {
                    die "Could not update $package with $ver{available}! (s/"$package"\s*:\s*"$ver{defined}"/"$package": "$ver{available}"/gxms)\n";
                }
            }
        }
    }
    path('package.json')->spew($pkg);
}

sub get_regestry {
    my ($group) = @_;
    $group =~ s{/.*$}{:};
    my $npm = `npm config list --json`;
    $npm = decode_json($npm);

    return $npm->{"${group}registry"} || $npm->{registry} || 'http://registry.npmjs.org/';
}

sub package_details {
    my ($dependency) = @_;
    my $registry = get_regestry($dependency);

    return eval {
        $mech->get($registry . $dependency);
        decode_json($mech->content());
    } || {};
}

__DATA__

=head1 NAME

npm-updater - Show what npm modules can be updated (and attempt to update to the latest where possible)

=head1 VERSION

This documentation refers to npm-updater version 0.0.1

=head1 SYNOPSIS

   npm-updater [option]

 OPTIONS:
  -c --cache[=]dir   Directory to store cached npmjs.com calls
  -p --pre-release   Include pre-release versions
  -u --update[=](major|minor|patch)
                     Update all package with changes in the major, minor or patch level

  -v --verbose       Show more detailed option
     --version       Prints the version information
     --help          Prints this help information
     --man           Prints the full documentation for npm-updater

=head1 DESCRIPTION

=head1 SUBROUTINES/METHODS

=head1 DIAGNOSTICS

=head1 CONFIGURATION AND ENVIRONMENT

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

=head1 BUGS AND LIMITATIONS

There are no known bugs in this module.

Please report problems to Ivan Wills (ivan.wills@gmail.com).

Patches are welcome.

=head1 AUTHOR

Ivan Wills - (ivan.wills@gmail.com)

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2016 Ivan Wills (14 Mullion Close, Hornsby Heights, NSW Australia 2077).
All rights reserved.

This module is free software; you can redistribute it and/or modify it under
the same terms as Perl itself. See L<perlartistic>.  This program is
distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.

=cut
